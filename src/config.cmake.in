@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

set(_@MLN_QT_NAME@_supported_components @MLN_QT_SUPPORTED_COMPONENTS@)

foreach(_comp ${@MLN_QT_NAME@_FIND_COMPONENTS})
    if (NOT _comp IN_LIST _@MLN_QT_NAME@_supported_components)
        set(@MLN_QT_NAME@_FOUND False)
        set(@MLN_QT_NAME@_NOT_FOUND_MESSAGE "Unsupported component: ${_comp}")
    endif()

    if (_comp STREQUAL Core)
        find_dependency(Qt@QT_VERSION_MAJOR@ COMPONENTS Gui Network)
        if(NOT @MLN_QT_WITH_INTERNAL_SQLITE@)
            find_dependency(Qt@QT_VERSION_MAJOR@ COMPONENTS Sql)
        endif()
    elseif(_comp STREQUAL Location)
        find_dependency(@MLN_QT_NAME@ COMPONENTS Core)

        find_dependency(Qt@QT_VERSION_MAJOR@ COMPONENTS Location)

        include("${CMAKE_CURRENT_LIST_DIR}/@MLN_QT_NAME@${_comp}Macros.cmake")
        include("${CMAKE_CURRENT_LIST_DIR}/@MLN_QT_NAME@${_comp}PluginGeoServicesTargets.cmake")
    elseif(_comp STREQUAL Widgets)
        find_dependency(@MLN_QT_NAME@ COMPONENTS Core)

        if (@QT_VERSION_MAJOR@ EQUAL 6)
            find_dependency(Qt@QT_VERSION_MAJOR@ COMPONENTS OpenGLWidgets Widgets)
        else()
            find_dependency(Qt@QT_VERSION_MAJOR@ COMPONENTS OpenGL Widgets)
        endif()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/@MLN_QT_NAME@${_comp}Targets.cmake")
endforeach()
